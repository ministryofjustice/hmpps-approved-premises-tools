docker_compose("./docker-compose.yml")

config.define_bool("local-api")
config.define_bool("local-ui")

cfg = config.parse()

local_api = cfg.get("local-api", False)
local_ui = cfg.get("local-ui", False)

resources = [
    "api",
    "frontend",
    "wiremock",
    "postgres",
    "redis",
    "localstack",
]

if local_api:
    resources.remove("api")
    local_resource(
        "local_api",
        serve_cmd="./gradlew bootRunDebug --stacktrace",
        # Note! These checks only wait for containers to start, ideally we'd wait for them to be ready
        # see https://docs.tilt.dev/resource_dependencies.html
        # "For dc_resource: the container is started (NB: Tilt doesnâ€™t currently observe docker-compose health checks)"
        resource_deps=["postgres", "redis", "localstack", "wiremock"],
        serve_dir=os.getenv("LOCAL_CAS_API_PATH"),
        readiness_probe=probe(
            period_secs=15, http_get=http_get_action(port=8080, path="/health")
        ),
        serve_env={
            "BOOT_RUN_ENV_FILE": os.getcwd() + '/.env.api'
        }
    )
    resources.append("local_api")

if local_ui:
    resources.remove("frontend")

    npm_command='export $(cat ' + os.getcwd() + '/.env.ui) && npm run start:dev'

    local_resource(
        "local_ui",
        cmd="npm install",
        serve_cmd=npm_command,
        serve_dir=os.getenv("LOCAL_CAS_UI_PATH"),
        dir=os.getenv("LOCAL_CAS_UI_PATH"),
        resource_deps=["redis"],
        readiness_probe=probe(
            period_secs=15, http_get=http_get_action(port=3000, path="/health")
        )
    )
    resources.append("local_ui")

config.clear_enabled_resources()
config.set_enabled_resources(resources)
